apply plugin: "java"
apply plugin: "groovy"

version = "1.01"

repositories {
  mavenCentral()
}

compileJava.options.encoding = "UTF-8"

allprojects {
  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"
}

ext {
  guiceVersion = "4.0"
  mongoLinkVersion = "1.2.2"
}

test {
  testLogging {
    exceptionFormat = "full"
    events "passed", "skipped", "failed"
    showStandardStreams = true
  }
  environment "AGFAPHD_API_MONGO_URI", "mongodb://localhost:27017/debts-test"
}

def libs = [
    commandValidator    : "org.hibernate:hibernate-validator:5.2.1.Final",
    fluentHttp          : "net.code-story:http:2.104",
    fluentRestTest      : "net.code-story:fluent-rest-test:1.5",
    fongo               : "com.github.fakemongo:fongo:1.6.3",
    gmongo              : "com.gmongo:gmongo:1.5",
    guava               : "com.google.guava:guava:18.0",
    guice               : "com.google.inject:guice:$guiceVersion",
    guiceMultibindings  : "com.google.inject.extensions:guice-multibindings:$guiceVersion",
    jacksonJoda         : "com.fasterxml.jackson.datatype:jackson-datatype-joda:2.6.3",
    javassist           : "org.javassist:javassist:3.20.0-GA",
    javaxEl             : "org.glassfish.web:javax.el:2.2.5",
    javaxElApi          : "javax.el:javax.el-api:2.2.5",
    jodaTime            : "joda-time:joda-time:2.9",
    jongo               : "org.jongo:jongo:1.3.0",
    junit               : "junit:junit:4.12",
    logback             : "ch.qos.logback:logback-classic:1.1.3",
    mockito             : "org.mockito:mockito-core:2.0.31-beta",
    mongoLink           : "org.mongolink:mongolink:$mongoLinkVersion",
    mongoLinkTestTools  : "org.mongolink:mongolink-testtools:$mongoLinkVersion",
    reflections         : "org.reflections:reflections:0.9.10",
    slf4j               : "org.slf4j:jul-to-slf4j:1.7.12",
    spock               : "org.spockframework:spock-core:1.0-groovy-2.4"
]

dependencies {
  compile libs.guava
  compile libs.guice
  compile libs.guiceMultibindings
  compile(libs.reflections) {
    exclude group: "org.javassist"
  }
  compile libs.javassist

  compile(libs.fluentHttp) {
    exclude module: "slf4j-simple"
  }

  compile libs.jodaTime
  compile libs.jacksonJoda

  compile libs.commandValidator
  compile libs.javaxEl
  compile libs.javaxElApi

  compile libs.mongoLink
  compile libs.jongo

  compile libs.slf4j
  compile libs.logback

  testCompile(libs.junit) {
    transitive = false
  }
  testCompile libs.mockito
  testCompile libs.spock
  testCompile libs.fluentRestTest
  testCompile libs.mongoLinkTestTools
  testCompile libs.fongo
  testCompile libs.gmongo
}

task wrapper(type: Wrapper) {
  gradleVersion = "2.8"
}

task deps(type: Sync) {
  from configurations.compile
  into "$buildDir/deps"
}

task stage(dependsOn: ["clean", "deps", "jar"])